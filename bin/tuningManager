#!/usr/bin/perl

use strict;
use lib "$ENV{GUS_HOME}/lib/perl";
use Data::Dumper;
use XML::Simple;
use DBI;
use Getopt::Long qw(GetOptions);
use TuningManager::TuningManager::InternalTable;
use TuningManager::TuningManager::ExternalTable;
use TuningManager::TuningManager::ExternalTuningTable;
use TuningManager::TuningManager::TuningIndex;
use TuningManager::TuningManager::Log;
use TuningManager::TuningManager::TableSuffix;
use TuningManager::TuningManager::Utils;

my $subversionDir;
my $props;
my $startTime = time;
my $maxRebuildMinutes = 0;
my $dbaEmail;
my $fromEmail;

my ($sec,$min,$hour,$mday,$mon,$year) = localtime();
my $timestamp = sprintf('%4d-%02d-%02d %02d:%02d:%02d', $year+1900, $mon+1, $mday, $hour, $min, $sec);

my $osUser = `whoami`;
chomp($osUser);
TuningManager::TuningManager::Log::addLogPreamble("Starting tuningManager as OS user \"$osUser\", "
					       . TuningManager::TuningManager::Log::getProcessInfo()
					       . " at $timestamp with arguments:\n"
					       . join(' ', @ARGV));

my $noArgs = !@ARGV; # check for no arguments now, before GetOptions consumes @ARGV

my ($configFile, $propFile, $instance, $schema, $password, $tables,
    $doUpdate, $debug, $notifyEmail, $cleanupWarnOnly, $cleanupAge, $forceUpdate,
    $maxWait, $logfile, $prefix, $filterValue);

GetOptions("configFile=s" => \$configFile,
           "propFile=s" => \$propFile,
           "instance=s" => \$instance,
           "schema=s" => \$schema,
           "password=s" => \$password,
           "tables=s" => \$tables,
           "doUpdate!" => \$doUpdate,
           "forceUpdate!" => \$forceUpdate,
           "notifyEmail=s" => \$notifyEmail,
           "debug!" => \$debug,
           "cleanupAge:i" => \$cleanupAge,
           "cleanupWarnOnly!" => \$debug,
           "maxWait=s" => \$maxWait,
           "logfile=s" => \$logfile,
           "prefix=s" => \$prefix,
           "filterValue=s" => \$filterValue,
	  );

TuningManager::TuningManager::Log::setLogfile($logfile);

TuningManager::TuningManager::Log::setDebugFlag()
  if $debug;

TuningManager::TuningManager::Log::addErrorLog("parameter error: \"propFile\" must be specified")
  unless ($propFile);
TuningManager::TuningManager::Log::addErrorLog("parameter error: \"instance\" must be specified")
  unless $instance;
TuningManager::TuningManager::Log::addErrorLog("parameter error: \"configFile\" must be specified")
  unless $configFile;
&usage unless ($instance && $propFile && $configFile);

exit if ($noArgs); # don't send a log email if they only want usage

$cleanupAge = 0 if !defined $cleanupAge;

$doUpdate = 1 if $forceUpdate;

my ($dbh, $brokenCount, $neededUpdateCount);
eval {
  if ($propFile) {
    my $simple = XML::Simple->new();
    $props = $simple->XMLin($propFile);
    my %permittedProperties = (password => 1,
                               maxRebuildMinutes => 1,
                               dbaEmail => 1,
                               fromEmail => 1,
                               dblink => 1,
                               schema => 1,
                               housekeepingSchema => 1,
                               logTable => 1,
                              );
    my $badProp;
    foreach my $foundProp (keys(%{$props})) {
      if (!$permittedProperties{$foundProp}) {
	TuningManager::TuningManager::Log::addErrorLog("invalid property \"$foundProp\"");
	$badProp = 1;
      }
    }

    my @requiredProperties = ('password',
                              'fromEmail',
                              'housekeepingSchema',
                              'logTable',
                              );
    foreach my $requiredProp (@requiredProperties) {
      if (!($props->{$requiredProp})) {
	TuningManager::TuningManager::Log::addErrorLog("required property \"$requiredProp\" not found");
	$badProp = 1;
      }
    }

    die if $badProp;
  }

  # prefer schema from command line, then prop file, then hardwired default
  $schema = $props->{schema} if !$schema;
  $schema = 'TuningManager' if !$schema;

  $maxRebuildMinutes = $props->{maxRebuildMinutes};

  $dbaEmail = $props->{dbaEmail} if $props->{dbaEmail};
  $fromEmail = $props->{fromEmail} if $props->{fromEmail};

  TuningManager::TuningManager::Log::addLog("creating tuning tables with the prefix \"$prefix\"")
      if $prefix;

  $SIG{INT} = \&catch_signal;
  $SIG{TERM} = \&catch_signal;

  ($instance, $schema, $password) = TuningManager::TuningManager::Utils::getDbLoginInfo($instance, $propFile, $schema, $password);
  TuningManager::TuningManager::Log::addLog("Logging in to the database as (and creating tuning tables in) the schema \"$schema\".");
  $dbh = TuningManager::TuningManager::Utils::getDbHandle($instance, $schema, $password);

  # check that the XML validation program is installed and runnable
  my $validationProgram = "validateXmlWithRng";
  TuningManager::TuningManager::Log::addErrorLog("failed to run XML validation program \"$validationProgram\"")
      unless `which $validationProgram`;

  # validate config file
  TuningManager::TuningManager::Log::addLog("Using config file \"$configFile\"");
  my $validationCmd = "$validationProgram $ENV{GUS_HOME}/lib/rng/tuningManager.rng $configFile |";
  TuningManager::TuningManager::Log::addLog("Validating XML:\n" . $validationCmd);
  open(PROGRAM, $validationCmd);
  while (<PROGRAM>) {
    my $line = $_;
    chomp($line);
    TuningManager::TuningManager::Log::addLog($line);
  }
  close(PROGRAM);
  my $exitCode = $? >> 8;
  TuningManager::TuningManager::Log::addErrorLog("invalid config file") if $exitCode;
#   die "invalid config file" if $exitCode;

  my ($config) = &parseConfigFile($configFile);

  # create an object for each <tuningTable> entity
  my %tuningTables;
  my %externalTables;
  my %externalTuningTables;

  foreach my $tuningTable (@{$config->{tuningTable}}) {

    $tuningTables{$tuningTable->{name}}
      = TuningManager::TuningManager::InternalTable->
	new($tuningTable->{name}, $tuningTable->{internalDependency}, $tuningTable->{externalDependency},
	    $tuningTable->{externalTuningTableDependency}, $tuningTable->{intermediateTable},
            $tuningTable->{ancillaryTable}, $tuningTable->{sql}, $tuningTable->{perl},
            $tuningTable->{unionization}, $tuningTable->{program}, $dbh, $debug,
            $tuningTable->{alwaysUpdate}, $tuningTable->{prefixEnabled},
            $maxRebuildMinutes, $instance, $propFile, $schema, $password,
            $subversionDir, $props->{dblink}, $props->{housekeepingSchema}, $props->{logTable});
  }

  if ($doUpdate && !$prefix) {
    clearDeadLock();
    checkLockout($maxWait) if $maxWait;
    $doUpdate = 0 if lockForUpdate($forceUpdate) eq "fail";
  }

  # validate config / check external files
  TuningManager::TuningManager::Log::addLogBanner("Using configuration file and checking external files");
  my $undefinedInternal;
  my $nonexistentExternal;
  my $nonexistentExternalTuningTable;
  my $dependencyCycle;
  foreach my $tableName (keys(%tuningTables)) {

    # check for internalDependencies that aren't in the list of tuningTables
    if ($tuningTables{$tableName}->getInternalDependencyNames()) {
      foreach my $dependency (@{$tuningTables{$tableName}->getInternalDependencyNames()}) {
	if (!$tuningTables{$dependency->{name}}) {
	  TuningManager::TuningManager::Log::addErrorLog("$tableName has an InternalDependency on $dependency->{name}, for which no description is supplied");
	  $undefinedInternal = 1;
	} else {
	  # add object to list of internal dependencies
	  $tuningTables{$tableName}->addInternalDependency($tuningTables{$dependency->{name}});
	}
      }
    }

    my $housekeepingSchema = $props->{housekeepingSchema};

    # check for externalDependencies that aren't in the database
    if ($tuningTables{$tableName}->getExternalDependencyNames()) {
      foreach my $dependency (@{$tuningTables{$tableName}->getExternalDependencyNames()}) {

	# new ExternalTable instance in the hash, if we haven't seen this
	my $key = $dependency->{name} . '@' . $dependency->{dblink};
	$externalTables{$key}
	  = TuningManager::TuningManager::ExternalTable->
	    new($dependency->{name}, $dependency->{dblink}, $dbh, $doUpdate, $housekeepingSchema)
	      unless ($externalTables{$key});

	# add external-table object to list of external dependencies
	$tuningTables{$tableName}->addExternalDependency($externalTables{$key});

	if (!$externalTables{$key}->exists()) {
	  TuningManager::TuningManager::Log::addErrorLog("$tableName has an ExternalDependency on $dependency->{name}, but no such table exists");
	  $nonexistentExternal = 1;
	}
      }
    }

    # check for externalTuningTableDependencies that aren't in the database
    if ($tuningTables{$tableName}->getExternalTuningTableDependencyNames()) {
      foreach my $dependency (@{$tuningTables{$tableName}->getExternalTuningTableDependencyNames()}) {

	# new ExternalTuningTable instance in the hash, if we haven't seen this
	my $key = $dependency->{name} . '@' . $dependency->{dblink};
	$externalTuningTables{$key}
	  = TuningManager::TuningManager::ExternalTuningTable->
	    new($dependency->{name}, $dependency->{dblink}, $dbh, $doUpdate, $housekeepingSchema)
	      unless ($externalTuningTables{$key});

	# add externalTuning-table object to list of externalTuning dependencies
	$tuningTables{$tableName}->addExternalTuningTableDependency($externalTuningTables{$key});

	if (!$externalTuningTables{$key}->exists()) {
	  TuningManager::TuningManager::Log::addErrorLog("$tableName has an ExternalTuningDependency on $key, but no such table exists");
	  $nonexistentExternalTuningTable = 1;
	}
      }
    }
  }

  # check for cycles in graph of dependencies
  foreach my $table (keys(%tuningTables)) {
    $dependencyCycle = 1
      if $tuningTables{$table}->hasDependencyCycle([]);
  }

  if ($undefinedInternal || $nonexistentExternal || $nonexistentExternalTuningTable || $dependencyCycle) {
    TuningManager::TuningManager::Log::addErrorLog("INVALID CONFIGURATION -- NOT UPDATING DATABASE");
    $doUpdate = 0;
  } else {
    TuningManager::TuningManager::Log::addLog("Configuration file OK");
  }

  # indexes
  TuningManager::TuningManager::Log::addLogBanner("Checking indexes");
  my $indexesConfigured;
  my $missingIndexes;
  foreach my $tuningIndex (@{$config->{tuningIndex}}) {

    $indexesConfigured++;
    my $indexObject =
      TuningManager::TuningManager::TuningIndex->
	  new($tuningIndex->{name},
	      $tuningIndex->{table},
	      $tuningIndex->{content},
	      $dbh);

    if (!$indexObject->exists()) {
      $missingIndexes++;
      TuningManager::TuningManager::Log::addLog("must " . $indexObject->getCreateStatement());

      $indexObject->create($dbh)
	if $doUpdate;
    }
  }

  # tables
  my $tableCount = 0;
  $brokenCount = 0;
  $neededUpdateCount = 0;
  my $upToDateCount = 0;

  TuningManager::TuningManager::Log::addLogBanner("Checking whether tuning tables are up to date");
  # check (and potentially update) every tuning table
  my @tableList;
  if ($tables) {
    @tableList = split(/,/, $tables);
  } else {
    @tableList = keys(%tuningTables);
  }

  foreach my $table (@tableList) {
    if (!$tuningTables{$table}) {
      TuningManager::TuningManager::Log::addErrorLog("unknown tuning table $table");
      next;
    }

    $tableCount++;
    my $state = $tuningTables{$table}->getState($doUpdate, $dbh, !$cleanupAge, $prefix, $filterValue);

    if ($state eq "broken") {
      $brokenCount++;
    } elsif ($state eq "neededUpdate") {
      $neededUpdateCount++;
    } elsif ($state eq "up-to-date") {
      $upToDateCount++;
    } else {
      TuningManager::TuningManager::Log::addErrorLog("invalid state $state");
    }
  }

  # cleanup
  TuningManager::TuningManager::Log::addLogBanner("Cleanup");
  cleanup($dbh, $cleanupAge, $cleanupWarnOnly, $prefix);

  &checkForUnusableIndexes;

  # summary
  if (!$tableCount) {
    TuningManager::TuningManager::Log::addLog("No tuningTable entities were defined in the config file");
  } else {
    my $summaryString = "\n$tableCount table(s) defined: $upToDateCount up-to-date, $neededUpdateCount "
      . ($doUpdate ? "updated" : "needing update")
	. ", $brokenCount broken";
    $summaryString .= " -- tuningManager ran successfully"
      if !TuningManager::TuningManager::Log::getErrorsEncounteredFlag();
    TuningManager::TuningManager::Log::addLogPreamble($summaryString);
  }

  # list broken tables
  if ($brokenCount) {
    TuningManager::TuningManager::Log::addLogPreamble("   broken table(s):");
    foreach my $table (@tableList) {
      TuningManager::TuningManager::Log::addLogPreamble("     " . $tuningTables{$table}->getName())
	  if $tuningTables{$table}->getState($doUpdate, $dbh) eq "broken";
    }
  }


  # list updated/outdated tables
  if ($neededUpdateCount) {
    if ($doUpdate) {
      TuningManager::TuningManager::Log::addLogPreamble("   table(s) updated:");
    } else {
      TuningManager::TuningManager::Log::addLogPreamble("   table(s) needing update:");
    }
    foreach my $table (@tableList) {
      TuningManager::TuningManager::Log::addLogPreamble("     " . $tuningTables{$table}->getName())
	  if $tuningTables{$table}->getState($doUpdate, $dbh) eq "neededUpdate";
    }
  }

  if (!$indexesConfigured) {
    TuningManager::TuningManager::Log::addLogPreamble("No tuningIndex entities were defined in the config file");
  } elsif ($missingIndexes) {
    TuningManager::TuningManager::Log::addLogPreamble($missingIndexes . " of the "
						   . $indexesConfigured
						   . " indexes configured were found to be missing");
  } else {
    TuningManager::TuningManager::Log::addLogPreamble("All "
						   . $indexesConfigured
						   . " indexes were found to exist.");
  }

  releaseLockForUpdate() if !$prefix;

  # if a temporary config-file dir was checked out from svn, delete it now
  if ($subversionDir && !$prefix) {
    my @cmdLine = ("rm", "-rf", $subversionDir);
    system(@cmdLine) == 0
      or TuningManager::TuningManager::Log::addErrorLog("unable to remove temporary config directory");
  }

};

# log any errors inside eval
TuningManager::TuningManager::Log::addErrorLog($@)
  if $@;

my $elapsed = time - $startTime;
my $hms = sprintf('%02d:%02d:%02d', $elapsed / 3600, ($elapsed % 3600) / 60, $elapsed % 60);

TuningManager::TuningManager::Log::addLogPreamble(time - $startTime .
					       " seconds to run tuningManager ($hms)\n\n");

TuningManager::TuningManager::Log::mailLog($notifyEmail, $instance)
  if $notifyEmail;


TuningManager::TuningManager::Log::mailOutOfSpaceReport($instance, $dbaEmail, $fromEmail)
  if TuningManager::TuningManager::Log::getOutOfSpaceMessage();

die if TuningManager::TuningManager::Log::getErrorsEncounteredFlag();

###############################################################################

sub usage {
  TuningManager::TuningManager::Log::addLog(
q{
Create/update "tuning tables", denormalized copies of data for better web site performance.

Usage: tuningManager -configFile <configXmlPath> -propFile <filename>
                      -instance <dbInstance>
                     {-doUpdate} {-notifyEmail <emailAddressList>} { -debug }
                     {-cleanupWarnOnly} {-cleanupAge <days>} {-forceUpdate}
                     {-prefix <prefix>} {-filterValue <value>}

options:
  configFile      an xml file describing the tables to be created
  propFile        file contains XML tuningProps entity containing password, schema, dblink, dbaEmail, or maxRebuildMinutes entities
  instance        the database instance to login and craete objects in
  schema          the schema (database user) to login and craete objects in
  tables          check/update only listed tables (and their antecedants)
  doUpdate        update any tables found to be out of date
  notifyEmail     comma-separated list of email addresses to send log to. ("none" to suppress email)
  cleanupWarnOnly name but don't drop obsolete tuning tables
  cleanupAge      specify how long (in days) tuning table must be obsolete before dropping (default 0)
  forceUpdate     perform update even if current_updater flag is set (implies -doUpdate)
  maxWait         limit, in hours, to wait if another tuning manager is updating the database
  debug           print debugging info
  prefix          create tuning tables and synonyms with supplied prefix, by substituting this value
                  for any occurrances of the "&prefix" macro
  filterValue     substitute this value for any occurences in the XML of the "&filterValue" macro

note:
  The -instance, -configFile, and -propFile parameters are required.

example:

tuningManager -instance <db instance> -propFile <propFile> -configFile <XML file>
});
}


sub parseConfigFile {
  my ($configFile) = @_;

  open(FILE, $configFile) || die "can't open configuration file '$configFile' for reading\n";
  my $simple = XML::Simple->new();

  # use forcearray so elements with one child are still arrays
  # and use keyattr so that unions are given as an ordered list
  # rather than a hash
  my $config = $simple->XMLin($configFile,
			    forcearray => 1,
			    KeyAttr => {sqlValue=>'+name'});
  if ($debug) {
    print Dumper($config);
    print "\n\n";
  }

  return ($config);
}

sub cleanup {
  my ($dbh, $cleanupAge, $cleanupWarnOnly, $prefix) = @_;
  my $cleanupCount = 0;
  my $housekeepingSchema = $props->{housekeepingSchema};

  # drop any tables that are listed as obsolete more than the threshold
  #   and don't have a synonym pointed at them
  my $sql = <<SQL;
select ott.name,
       to_char(sysdate - ott.timestamp, '9.99') as days_old
from $housekeepingSchema.ObsoleteTuningTable ott,
     all_tables at
where ott.timestamp < sysdate - $cleanupAge
  and ott.name = at.owner || '.' || at.table_name
  and lower(ott.name) not in
        (select lower(table_owner || '.' || table_name) from all_synonyms)
SQL
  TuningManager::TuningManager::Log::addLog($sql) if $debug;

  my $select = $dbh->prepare($sql);
  $select->execute()
    or TuningManager::TuningManager::Log::addErrorLog("\n" . $dbh->errstr . "\n");

  # prepare sql to delete ObsoleteTuningTable record when OTT is dropped
  my $sql = <<SQL;
delete from $housekeepingSchema.ObsoleteTuningTable
where name = ?
SQL
  my $delete  = $dbh->prepare($sql);

  while (my ($dropee, $daysOld) = $select->fetchrow_array()) {
    $cleanupCount++;
    my $dropMsg = "$dropee obsolete for $daysOld days.";

    if (!$cleanupWarnOnly) {
      $dropMsg .= " . . .dropping.";
      my $sql = <<SQL;
          drop table $dropee
SQL
      my $drop = $dbh->prepare($sql);
      $drop->execute()
	or TuningManager::TuningManager::Log::addErrorLog("\n" . $dbh->errstr . "\n");
      $drop->finish();

      $delete->execute($dropee)
	or TuningManager::TuningManager::Log::addErrorLog("\n" . $dbh->errstr . "\n");
    }
    TuningManager::TuningManager::Log::addLog($dropMsg);
  }
  $select->finish();
  $delete->finish();

  # check for obsolete tuning tables that begin with $prefix (whether or not
  # that's the empty string) and are not listed in $housekeepingSchema.ObsoleteTuningTable.
  # If $prefix is defined, drop any matching obsolete unlisted tuning tables.
  # (That's safe because the workflow will prevent two instances of the same
  # organism's tuning manager from running at the same time.) If $prefix isn't
  # defined (i.e. we aren't in a workflow), issue a warning.
  my $newTablesTerm = '';
  if (TuningManager::TuningManager::TableSuffix::suffixDefined()) {
    my $suffix = TuningManager::TuningManager::TableSuffix::getSuffix($dbh);
    $newTablesTerm = "and atab.table_name not like '%$suffix%'";
  }
  # note that the backslash in the regex is to protect the dollar-sign from Perl
  # it's really "zero or more digits, followed by end-of-line": [0-9]*$
  $sql = <<SQL;
select owner || '.' || table_name
from (select atab.owner, atab.table_name
      from all_synonyms asyn, all_tables atab
      where asyn.owner = atab.owner
        and regexp_replace(atab.table_name, '[0-9]*\$', '') = asyn.synonym_name
        and atab.table_name like upper('$prefix\%')
        $newTablesTerm
    minus select table_owner, table_name from all_synonyms)
minus select name from $housekeepingSchema.ObsoleteTuningTable
SQL

  my $stmt = $dbh->prepare($sql);
  $stmt->execute()
    or TuningManager::TuningManager::Log::addErrorLog("\n" . $dbh->errstr . "\n");
  while (my ($table) = $stmt->fetchrow_array()) {
    if ($prefix) {
      TuningManager::TuningManager::Log::addLog("Dropping obsolete tuning table $table, which is not listed in $housekeepingSchema.ObsoleteTuningTable.");
      $dbh->do("drop table $table")
	or TuningManager::TuningManager::Log::addErrorLog("\n" . $dbh->errstr . "\n");
    } else {
      TuningManager::TuningManager::Log::addLog("WARNING: Obsolete tuning table $table is not listed in $housekeepingSchema.ObsoleteTuningTable. . . consider dropping it");
    }
  }

  $stmt->finish();

  # check for $housekeepingSchema.ObsoleteTuningTable records whose tables don't exist
  $sql = <<SQL;
select name from $housekeepingSchema.ObsoleteTuningTable
minus select owner || '.' || table_name
from (select atab.owner, atab.table_name
      from all_synonyms asyn, all_tables atab
      where asyn.owner = atab.owner
        and regexp_replace(atab.table_name, '[0-9]*\$', '') = asyn.synonym_name)
SQL

  my $stmt = $dbh->prepare($sql);
  $stmt->execute()
    or TuningManager::TuningManager::Log::addErrorLog("\n" . $dbh->errstr . "\n");
  while (my ($table) = $stmt->fetchrow_array()) {
    TuningManager::TuningManager::Log::addErrorLog("tuning table $table is listed in $housekeepingSchema.ObsoleteTuningTable but does not exist");
  }
  $stmt->finish();

  TuningManager::TuningManager::Log::addLog("$cleanupCount obsolete tables found")
      if $cleanupCount;

  TuningManager::TuningManager::Log::addLog("*** NOTE: obsolete tables retained because -cleanupWarnOnly parameter is set ***")
      if $cleanupCount && $cleanupWarnOnly;
}

sub lockForUpdate {
  my ($forceUpdate) = @_;
  my $housekeepingSchema = $props->{housekeepingSchema};

  my $processInfo  = TuningManager::TuningManager::Log::getProcessInfo();

  my $sql = <<SQL;
    update $housekeepingSchema.InstanceMetaInfo
    set current_updater = '$processInfo', update_start = sysdate
SQL

  $sql .= " where current_updater is null"
    unless $forceUpdate;

  $dbh->do($sql)
    or TuningManager::TuningManager::Log::addErrorLog("\n" . $dbh->errstr . "\n");

  my $stmt = $dbh->prepare("select current_updater from $housekeepingSchema.InstanceMetaInfo");
  $stmt->execute();
  my ($updater) = $stmt->fetchrow_array();

  if ($updater ne $processInfo) {
    my $updateLockoutMsg = <<MSG;
Another tuningManager process appears to be updating the database. CURRENT_UPDATER is set to "$updater".
Continuing in read-only mode, but not updating tuning tables. If this process ($updater) is not running, you can override this lockout by rerunning tuningManager with the "--forceUpdate" option.
MSG
    TuningManager::TuningManager::Log::addErrorLog($updateLockoutMsg);
    return("fail");
  }
  $stmt->finish();
}

sub releaseLockForUpdate {
  my $housekeepingSchema = $props->{housekeepingSchema};

  $dbh->do("update $housekeepingSchema.InstanceMetaInfo set current_updater = null, update_start = null where current_updater = '"
	   . TuningManager::TuningManager::Log::getProcessInfo()
	   . "'")
    or TuningManager::TuningManager::Log::addErrorLog("\n" . $dbh->errstr . "\n");
}

sub clearDeadLock {
  my $housekeepingSchema = $props->{housekeepingSchema};

  my $stmt = $dbh->prepare("select current_updater from $housekeepingSchema.InstanceMetaInfo");
  $stmt->execute();
  my ($updater) = $stmt->fetchrow_array();

  return if !$updater;

  if (!($updater =~ /process \d* on \S*/)) {
    TuningManager::TuningManager::Log::addErrorLog("ERROR: parsing current_updater string: \"" . $updater . "\"");
    return;
  }

  my @parts = split / /, $updater;
  my $pid = $parts[1];
  my $server = $parts[3];
  my $current_server = `uname -n`;
  chomp($current_server);

  if ($current_server ne $server) {
    TuningManager::TuningManager::Log::addErrorLog("can't check for tM process; it's on \"$server\" and I'm on \"$current_server\"");
    return;
  }

  my $pid_test = ` ps -fp $pid|tail -n +2|wc -l`;
  chomp($pid_test);
  if (!$pid_test) {
    # named process no longer exists; null out CURRENT_UPDATER
    TuningManager::TuningManager::Log::addLog("CURRENT_UPDATER process (" . $updater . ") no longer exists; updating InstanceMetaInfo.");
    $dbh->do("update $housekeepingSchema.InstanceMetaInfo set current_updater = null, update_start = null")
    or TuningManager::TuningManager::Log::addErrorLog("\n" . $dbh->errstr . "\n");
  }

  return;
}

sub catch_signal {
        my $signame = shift;
        die "caught signal $signame";
}

sub checkLockout {

  my ($maxWait) = @_;
  my $wait = 0;

  until ($wait > $maxWait) {

    my $stmt = $dbh->prepare(<<SQL);
        select nvl((sysdate - update_start) * 24, $maxWait + 1) -- if we are not locked out, force the wait value to greater than maxWait
        from InstanceMetaInfo
SQL

    $stmt->execute();
    ($wait) = $stmt->fetchrow_array();

    if ($wait < $maxWait) {
      my $dateString = `date`;
      chomp($dateString);
      TuningManager::TuningManager::Log::addLog("Waiting for another tuning manager at $dateString");
      sleep (300);
    }
  }

}

sub checkForUnusableIndexes {
  my $stmt = $dbh->prepare(<<SQL);
                    select index_name
                    from all_indexes
                    where owner not in ('SYS', 'SYSTEM')
                      and status != 'VALID'
                      and (status != 'N/A'
                           or index_name in (select index_name
                                             from all_ind_partitions
                                             where status != 'USABLE'
                                               and (status != 'N/A'
                                                    or index_name in (select index_name
                                                                      from all_ind_subpartitions
                                                                      where status != 'USABLE'))))
SQL

  $stmt->execute();
  while (my ($index) = $stmt->fetchrow_array()) {
    TuningManager::TuningManager::Log::addErrorLog("index \"$index\" is unusable");
  }
  $stmt->finish();
}
